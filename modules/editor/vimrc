""""""""""""""""""""""""""""""""" ESSENTIALS """"""""""""""""""""""""""""""""""<
filetype plugin indent on                                                      " for plugins to load correctly
syntax enable                                                                  " turn on syntax highlighting
set nocompatible                                                               " don't try to be vi compatible

"""""""""""""""""""""""""""""""""" SETTINGS """""""""""""""""""""""""""""""""""<
" interface
set cmdheight=1                                                                " set command line height
set foldcolumn=1                                                               " always show at least one cahr in fold column
set hlsearch                                                                   " highlight search
set laststatus=2                                                               " always show last status
set noshowmode                                                                 " do not show mode in command line
set showmatch                                                                  " highlight matching brackets
set signcolumn=yes                                                             " always show sign column
set showfulltag                                                                " show info about tag in completemenu
set showcmd                                                                    " always show the command opearator
set ruler                                                                      " show file stats
set number                                                                     " show line numbers
set nospell                                                                    " do not display spellchecking
set list listchars=tab:»\ ,nbsp:␣,trail:·,extends:›,precedes:‹                 " show blank characters
set wrap                                                                       " wrap a text on screen
set shortmess+=a shortmess+=c                                                  " shorter messages

" behaviour
set autowrite                                                                  " autosave buffer when using jumps
set backspace=indent,eol,start                                                 " set the behavious of backspace
set clipboard=unnamedplus                                                      " linux standard clipboard
set encoding=utf-8                                                             " encoding
set termencoding=utf-8                                                         " encoding for terminal
set hidden                                                                     " allow hidden buffers
set incsearch                                                                  " show search results while typing regex
set matchpairs+=<:>                                                            " use % to jump between < and > as well
set mouse=a                                                                    " full mouse intagration
set undofile                                                                   " save history of the file
set nobackup                                                                   " no mess in the project
set noswapfile                                                                 " no problematic swap files
set undodir=/tmp/undodir                                                       " set the directory for undofiles

" completion
set omnifunc=syntaxcomplete#Complete                                           " set onmicomplete function
set completeopt=menuone,noselect                                               " on completion always show menu, but do not select anything

" diff options
set diffopt+=algorithm:minimal                                                 " most minimal diff possible
set diffopt+=indent-heuristic                                                  " indentation
set diffopt-=internal                                                          " don't use internal diff algorithm

" more natural splits
set splitbelow                                                                 " split below to current window
set splitright                                                                 " split right to current window

" indentation
set noshiftround                                                               " do not round indent to multiple of 2
set shiftwidth=2                                                               " indent size

" tabs into space
set expandtab                                                                  " tabs into spaces
set softtabstop=2                                                              " width of the tab in spaces
set tabstop=2                                                                  " tabs at 2 spaces

" text width
set textwidth=80                                                               " 80 columns wide text
set colorcolumn=80,160                                                         " a bar at 80 and 160 column

" gotta go fast!
set updatetime=300                                                             " CursorHold delay
set timeoutlen=1000 ttimeoutlen=0                                              " immediate escape key result
set ttyfast                                                                    " fast rendering

" autocomplete paths in command mode
set path+=**                                                                   " search down files into subdirectories
set wildmenu                                                                   " display all matching files on tab completion
set wildignore+=.git/,**/node_modules/**                                       " ignore files from unwanted directories
set wildmode=longest:full,full                                                 " match longest path fully matching pattern

" ctags
set tags+=.git/tags                                                            " add .git/tags file to scope

" status line
set statusline=%1*\ %{toupper(mode())}\ %2*\ %F:%l:%c%=
set statusline+=\ %Y\ %{LinterStatus()}\ %1*\ %m%r[%n]\ %{strlen(&fenc)?&fenc:'none'}\ 

autocmd InsertEnter,InsertLeave * set cul!

""""""""""""""""""""""""""""""""""" LEADERS """""""""""""""""""""""""""""""""""<
let g:mapleader      = "\<space>"                                              " main leader set to space
let g:maplocalleader = "\<backspace>"                                          " local leader set to comma

"""""""""""""""""""""""""""""""""""" NETRW """"""""""""""""""""""""""""""""""""<
" do not load shitty netrw
let g:loaded_netrwPlugin = 1
let g:loaded_netrw = 1

""""""""""""""""""""""""""""""""""" COPILOT """""""""""""""""""""""""""""""""""<
inoremap <silent><script><expr> <f11> copilot#Accept("\<cr>")
imap <F10> <Plug>(copilot-previous)
imap <F12> <Plug>(copilot-next)
imap <F9> <Plug>(copilot-suggest)
let g:copilot_no_tab_map = v:true

"""""""""""""""""""""""""""""""""""" CTRLP """"""""""""""""""""""""""""""""""""<
nnoremap <leader><leader> :CtrlP<cr>
nnoremap <localleader><localleader> :CtrlPBuffer<cr>
let g:ctrlp_match_window = 'bottom,order:btt,min:1,max:10,results:50'
let g:ctrlp_show_hidden = 1
let g:ctrlp_map = '<leader><leader>'
let g:ctrlp_switch_buffer = 'etvh'
let g:ctrlp_open_multiple_files = '2vjr'
let g:ctrlp_user_command = {
  \ 'types': {
    \ 1: ['.git', 'cd %s && git ls-files'],
    \ },
  \ 'fallback': 'find %s -type f'
  \ }

""""""""""""""""""""""""""""""""""""" TAGS """"""""""""""""""""""""""""""""""""<
command! -nargs=0 MakeTags call job_start('git ctags')

autocmd! BufWritePost * MakeTags

nnoremap <localleader>f g<c-]>
vnoremap <localleader>f g<c-]>

""""""""""""""""""""""""""""""""""""" MAKE """"""""""""""""""""""""""""""""""""<
nnoremap <silent><localleader>b :lmake file=%:.<cr>
nnoremap <silent><localleader>B :lmake file=%:.:<c-r>=line('.')<cr><cr>
nnoremap <silent><localleader>v :!<c-r>=&makeprg<cr> file=%:.<cr>
nnoremap <silent><localleader>V :!<c-r>=&makeprg<cr> file=%:.:<c-r>=line('.')<cr><cr>

augroup MakePrg
  autocmd BufEnter *_spec.rb setlocal makeprg=make\ testrb efm=rspec\ %f:%l\ #\ %m

  autocmd BufEnter *.test.js setlocal makeprg=make\ testjs efm=%.%#\ at\ %f:%l:%c,%.%#\ at\ %.%#(%f:%l:%c)
  autocmd BufEnter *.test.ts setlocal makeprg=make\ testjs efm=%.%#\ at\ %f:%l:%c,%.%#\ at\ %.%#(%f:%l:%c)
  autocmd BufEnter *.test.jsx setlocal makeprg=make\ testjs efm=%.%#\ at\ %f:%l:%c,%.%#\ at\ %.%#(%f:%l:%c)
  autocmd BufEnter *.test.tsx setlocal makeprg=make\ testjs efm=%.%#\ at\ %f:%l:%c,%.%#\ at\ %.%#(%f:%l:%c)
augroup END

""""""""""""""""""""""""""""""""""" ALTFILE """""""""""""""""""""""""""""""""""<
function! s:altFile()
  if exists("b:altfile")
    if bufwinid(b:altfile) > -1
      call win_gotoid(bufwinid(b:altfile))
    else
      silent execute ":vs" b:altfile
    endif
  endif
endfunction

nnoremap <localleader>x :call <sid>altFile()<cr>

augroup AltFile
  autocmd BufReadPost *.rb call setbufvar(expand('%'), 'altfile', expand('%:.:s/^app/spec/:s/\.rb$/_spec.rb/'))
  autocmd BufReadPost *_spec.rb call setbufvar(expand('%'), 'altfile', expand('%:.:s/^spec/app/:s/_spec\.rb$/.rb/'))

  autocmd BufReadPost *.js  call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.js$/.test.js/'))
  autocmd BufReadPost *.ts  call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.ts$/.test.ts/'))
  autocmd BufReadPost *.jsx call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.jsx$/.test.jsx/'))
  autocmd BufReadPost *.tsx call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.tsx$/.test.tsx/'))
  autocmd BufReadPost *.test.js  call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.test\.js$/.js/'))
  autocmd BufReadPost *.test.ts  call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.test\.ts$/.ts/'))
  autocmd BufReadPost *.test.jsx call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.test\.jsx$/.jsx/'))
  autocmd BufReadPost *.test.tsx call setbufvar(expand('%'), 'altfile', expand('%:.:s/\.test\.tsx$/.tsx/'))
augroup END

""""""""""""""""""""""""""""""""""""" ALE """""""""""""""""""""""""""""""""""""<
function! LinterStatus() abort
  let l:counts = ale#statusline#Count(bufnr(''))
  let l:all_errors = l:counts.error + l:counts.style_error
  let l:all_non_errors = l:counts.total - l:all_errors
  return l:counts.total == 0 ? 'OK' : printf('%dW %dE', all_non_errors, all_errors)
endfunction

let g:ale_close_preview_on_insert = 1
let g:ale_completion_enabled = 1
let g:ale_echo_msg_error_str = 'E'
let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'
let g:ale_echo_msg_warning_str = 'W'
let g:ale_floating_preview = 1
let g:ale_floating_window_border = ['│', '─', '╭', '╮', '╯', '╰', '│', '─']
let g:ale_sign_column_always = 1
let g:ale_sign_error = 'E'
let g:ale_sign_info = 'I'
let g:ale_sign_style_error = 'ES'
let g:ale_sign_style_warning = 'WS'
let g:ale_sign_warning = 'W'
let g:ale_virtualtext_cursor = 'current'

let g:ale_linters = {'haskell': ['hls','hlint','ormolu']}

set omnifunc=ale#completion#OmniFunc
inoremap <tab> <c-n>
inoremap <c-tab> <c-x><c-o>

nnoremap <silent>[e     :ALEPreviousWrap<cr>
nnoremap <silent>]e     :ALENextWrap<cr>

nnoremap <localleader>h :ALEHover<cr>
nnoremap <localleader>H :ALEDetail<cr>
nnoremap <localleader>a :ALECodeAction<cr>
nnoremap <localleader>d :ALEGoToDefinition<cr>
nnoremap <localleader>D :ALEGoToTypeDefinition<cr>
nnoremap <localleader>i :ALEInfo<cr>
nnoremap <localleader>I :ALEToggle<cr>
nnoremap <localleader>w :ALEPopulateLocList<cr>
nnoremap <localleader>F :ALEFindReferences<cr>

""""""""""""""""""""""""""""""""""" COMMENT """""""""""""""""""""""""""""""""""<
function! s:commentToggle()
  let line = getline('.')
  if match(l:line, '^\s*'.substitute(&commentstring, '%s', '.*', '')) >= 0
    let l:target = substitute(l:line, '^\s*\zs'.substitute(&commentstring, '%s', '', ''), '', '')
  else
    let l:target = printf(&commentstring, l:line)
  endif
  keeppatterns s/^.*$/\=l:target/
endfunction

nnoremap <silent><localleader>e :call <sid>commentToggle()<cr>
vnoremap <silent><localleader>e :call <sid>commentToggle()<cr>

""""""""""""""""""""""""""""""""""" QUICKFIX """"""""""""""""""""""""""""""""""<
function! s:openQuickfix(new_split_cmd)
  let l:qf_idx = line('.')
  wincmd p
  execute a:new_split_cmd
  execute l:qf_idx . 'cc'
endfunction

autocmd FileType qf nnoremap <buffer> <c-v> :call <sid>openQuickfix("vnew")<cr>
autocmd FileType qf nnoremap <buffer> <c-x> :call <sid>openQuickfix("split")<cr>
autocmd Filetype qf setlocal statusline=%1*%f%2*

" toggle loclist list
function! s:toggleLocList()
  if empty(filter(getwininfo(), 'v:val.loclist'))
    lopen
  else
    lclose
  endif
endfunction

nnoremap <silent><leader>w :call <sid>toggleLocList()<cr>

" toggle quickfix list
function! s:toggleQuickFix()
  if empty(filter(getwininfo(), 'v:val.quickfix'))
    copen
  else
    cclose
  endif
endfunction

nnoremap <silent><leader>q :call <sid>toggleQuickFix()<cr>

""""""""""""""""""""""""""""""""""""" TODO """"""""""""""""""""""""""""""""""""<
function! s:currentGitHubIssue()
  let l:branch = trim(system('git rev-parse --abbrev-ref HEAD'))
  let l:issue_no = matchstr(l:branch, '\v\d+')
  if !empty(l:issue_no)
    let l:issue = substitute(system("gh issue view --json title,body --jq '\"\\(.title)\\n\\n\\(.body)\"' " . l:issue_no), '\r', '', 'g')
    execute "0put =l:issue"
  endif
endfunction

function! s:toggleTodoFile()
  silent call system('git notes --ref=todo copy HEAD~ HEAD')
  if buflisted(bufname('.git/NOTES_EDITMSG'))
    if !win_gotoid(bufwinid('.git/NOTES_EDITMSG'))
      execute ":split .git/NOTES_EDITMSG"
    endif
  else
    execute ":G notes --ref=todo edit"
  endif
  execute ":set syntax=xit"
endfunction

nnoremap <leader>t :<c-u>call <sid>currentGitHubIssue()<cr>
nnoremap <leader>x :call <sid>toggleTodoFile()<cr>

""""""""""""""""""""""""""""""""""" FUGITIVE """"""""""""""""""""""""""""""""""<
function! s:toggleFugitive()
  if buflisted(bufname('fugitive:///*/.git//$'))
    execute ":bdelete" bufname('fugitive:///*/.git//$')
  else
    Git
    resize 10
    setlocal winfixheight
    setlocal nonumber
    setlocal norelativenumber
  endif
endfunction

function! s:pickaxe(query)
  if !empty(a:query)
    execute ":G log -p -G \"" a:query "\""
    call setreg("/", "\\V" . a:query)
  endif
endfunction

function! s:defaultBranch()
  return trim(system('git config --get core.default'))
endfunction

function! s:branchoffCommit()
  return trim(system('git merge-base ' . <sid>defaultBranch() . ' HEAD'))
endfunction

function! s:whatthecommit()
  return trim(system('curl -Ls whatthecommit.com/index.txt'))
endfunction

function! s:gitPruneFile()
  let file = expand('%')
  let branch_base = trim(system('git merge-base origin/HEAD HEAD'))
  execute "!git filter-branch --prune-empty --force --tree-filter 'rm -f " . l:file . "' " . l:branch_base . "..HEAD"
endfunction

function! s:gitWIP()
  execute ":G commit --no-verify --message 'WIP " . <sid>whatthecommit() . "'"
endfunction

function! s:gitRebaseBranchOnly()
  execute ":G rebase --interactive " . <sid>branchoffCommit()
endfunction

function! s:gitChanges()
  execute ":G log --pretty=oneline " . <sid>branchoffCommit() . "..HEAD"
  resize 10
endfunction

function! s:gitUpdate()
  G fetch
  execute ":G pull --rebase " . substitute(<sid>defaultBranch(), "/", " ", "")
endfunction

function! s:gitAbsorb()
  execute ":G absorb --base " . <sid>branchoffCommit()
endfunction

function! s:gitAyWayCommitMessage()
  execute ":%!cmc"
  redraw!
endfunction

command! -nargs=0 W Gwrite
command! -nargs=0 E Gedit
command! -nargs=0 D GDelete
command! -nargs=0 Gprune call <sid>gitPruneFile()
command! -nargs=0 Gwip call <sid>gitWIP()
command! -nargs=0 Grebase call <sid>gitRebaseBranchOnly()
command! -nargs=0 Gchanges call <sid>gitChanges()
command! -nargs=0 Gupdate call <sid>gitUpdate()
command! -nargs=0 Gabsorb call <sid>gitAbsorb()
command! -nargs=1 -complete=buffer GPickaxe call <sid>pickaxe(<q-args>)

autocmd! FileType fugitive nnoremap <buffer> rI :<c-u>call <sid>gitRebaseInteractive()<cr>
autocmd! FileType gitcommit setlocal spell spelllang=en_us
autocmd! FileType gitcommit nnoremap <buffer> <cr> :<c-u>call <sid>gitAyWayCommitMessage()<cr>

nnoremap <leader>a :Gabsorb<cr>
nnoremap <leader>b :G blame<cr>
nnoremap <leader>B :G branch<cr>
nnoremap <leader>e :Gedit<cr>
nnoremap <leader>o :Gupdate<cr>
nnoremap <leader>O :G push --force-with-lease --follow-tags<cr>
nnoremap <leader>g :call <sid>toggleFugitive()<cr>
nnoremap <leader>d :Gchanges<cr>
nnoremap <leader>C :0GlLog<cr>
nnoremap <leader>c :GcLog<cr>
nnoremap <leader>p :G fetch<cr>
nnoremap <leader>P :G pull<cr>
nnoremap <leader>s :call <sid>pickaxe(input("pickaxe /", "", "tag"))<cr>
vnoremap <leader>s :<c-u>call <sid>pickaxe(<sid>selectedText())<cr>
""""""""""""""""""""""""""""""""""" FUGITIVE """""""""""""""""""""""""""""""""">

"""""""""""""""""""""""""""""""""" GITGUTTER """"""""""""""""""""""""""""""""""<
nnoremap ]h :GitGutterNextHunk<cr>
nnoremap [h :GitGutterPrevHunk<cr>

"""""""""""""""""""""""""""""""" EXPAND REGION """"""""""""""""""""""""""""""""<
vmap v     <Plug>(expand_region_expand)
vmap <C-v> <Plug>(expand_region_shrink)

"""""""""""""""""""""""""""""""""" BLAMELINE """"""""""""""""""""""""""""""""""<
hi BlameLine ctermfg=8
call prop_type_add('blame_line', { 'highlight': 'BlameLine' })

function! BlameLineHandler(channel, msg)
  let text = substitute(trim(a:msg), '^\(\w\+\)\s\+(\(.\{-}\)\s\+\d\+).*', '\1 \2', 'g')
  call prop_remove({ 'type': 'blame_line' }, 1, line('$'))
  call prop_add(line('.'), 0, { 'text': l:text, 'type': 'blame_line', 'text_align': 'right' })
endfunc

function! s:blameLine()
  if isdirectory(".git")
    let line = line('.')
    let file = expand('%')
    let cmd = 'git blame --date=relative -L' . l:line . ',' . l:line . ' ' . l:file
    call job_start(l:cmd, { 'out_cb': 'BlameLineHandler' })
  endif
endfunction

autocmd CursorHold * call <sid>blameLine()

"""""""""""""""""""""""""""""""""""" UTILS """"""""""""""""""""""""""""""""""""<
" returns a text under visual selection
function! s:selectedText()
  silent normal gv"vy
  let l:selection = getreg('v')
  let l:escaped   = escape(l:selection, '\/')
  let l:regexp    = substitute(l:escaped, "\n", "\\\\n", "g")
  normal \<Esc>
  return l:regexp
endfunction

" triggers a buffer search using a given query
function! s:findTextInBuffer(register, query)
  call setreg(a:register, "\\V" . a:query)
  normal n
endfunction

" some appearance togglable settings
nnoremap <leader>1 :set relativenumber!<cr>
nnoremap <leader>2 :set hlsearch!<cr>
nnoremap <leader>3 :set spell!<cr>

" make folds and return to manual foldmethod
function! s:makeFolds()
  setlocal foldmethod=indent
  norm zR
  setlocal foldmethod=marker
endfunction

set foldmethod=marker
nnoremap <leader>4 :call <sid>makeFolds()<cr>

" terminal-mode
nnoremap <leader>v :vertical terminal<cr>
autocmd TerminalWinOpen * tmap <esc> <c-\><c-n>

" cnake case to camel case and back
nnoremap <silent><localleader>c :keeppatterns s:<c-r><c-w>:\=substitute(submatch(0), '\(\u\?\l\+\)\(\u\)', '\l\1_\l\2', 'g'):<cr><c-o>
nnoremap <silent><localleader>C :keeppatterns s:<c-r><c-w>:\=substitute(submatch(0),'\(\l\+\)_\?', '\u\1', 'g'):<cr><c-o>

" use `open` command to open a current buffer
command! -nargs=0 Open !open %

" leave only the current buffer - delete others
command! -nargs=0 BufOnly execute '%bdelete|edit #|normal `"'

" undo sequence for space, dot and newline
inoremap <space> <C-G>u<space>
inoremap . <C-G>u.
inoremap <cr> <C-G>u<cr>

" `<C-p>` that acts like `p`, but not change the `+` register
vnoremap <C-p> "pdp
vnoremap <C-P> "pdP

" right click is an escape
nnoremap <RightMouse> <esc>
inoremap <RightMouse> <esc>
vnoremap <RightMouse> <esc>
cnoremap <RightMouse> <esc>

"""""""""""""""""""""""""""""""""" NAVIGATION """""""""""""""""""""""""""""""""<
" triggers a grep on query and opens quickfix list
function! s:grep(query)
  if !empty(a:query)
    execute "silent Ggrep \"" . a:query . "\" | copen"
  endif
endfunction

" finds a file using git ls-files or find command
function! s:find(query)
  if !empty(a:query)
    if isdirectory(".git")
      execute "silent cexpr system('git ls-files \"**" . a:query . "*\" | sed \"s/.*/&:1:1/\"') | copen"
    else
      execute "silent cexpr system('find . -type f -printf \"%p:1:1\\n\" \"*" . a:query . "*\"') | copen"
    endif
  endif
endfunction

" move around buffers with leader key
nnoremap <localleader><cr>  :argadd<cr>
nnoremap <localleader><esc> :argdelete<cr>
nnoremap <localleader>h :previous<cr>
nnoremap <localleader>l :next<cr>
nnoremap <localleader>k :bprevious<cr>
nnoremap <localleader>j :bnext<cr>

" move around windows with leader key
nnoremap <silent><leader>k :wincmd k<cr>
nnoremap <silent><leader>j :wincmd j<cr>
nnoremap <silent><leader>h :wincmd h<cr>
nnoremap <silent><leader>l :wincmd l<cr>

" lookup
nnoremap <leader>F :call <sid>find(input("find /", "", "file_in_path"))<cr>
nnoremap <leader>f :call <sid>grep(input("grep /", "", "tag"))<cr>

vnoremap <leader>F :<c-u>call <sid>find(<sid>selectedText())<cr>
vnoremap <leader>f :<c-u>call <sid>grep(<sid>selectedText())<cr>

vnoremap <silent>* :<c-u>call <sid>findTextInBuffer("/", <sid>selectedText())<cr>n
vnoremap <silent># :<c-u>call <sid>findTextInBuffer("#", <sid>selectedText())<cr>n

command! -nargs=1 -complete=buffer Grep call <sid>grep(<q-args>)
command! -nargs=1 -complete=buffer Find call <sid>find(<q-args>)

"""""""""""""""""""""""""""""""""""" COLORS """""""""""""""""""""""""""""""""""<
hi ColorColumn                          ctermbg=8
hi CtrlPMode1                           ctermbg=0  ctermfg=3
hi DiffAdd                              ctermbg=8  ctermfg=10 cterm=none
hi DiffChange                           ctermbg=8  ctermfg=3  cterm=none
hi DiffDelete                           ctermbg=8  ctermfg=9  cterm=none
hi DiffText                             ctermbg=8  ctermfg=11 cterm=underline
hi Directory                                       ctermfg=4
hi FoldColumn                           ctermbg=0  ctermfg=7
hi Folded                               ctermbg=0  ctermfg=7  cterm=bold
hi NormalFloat                          ctermbg=0
hi Pmenu                                ctermbg=0  ctermfg=7
hi PmenuSbar                            ctermbg=0  ctermfg=7
hi PmenuSel                             ctermbg=8  ctermfg=15
hi QuickFixLine                         ctermbg=8
hi Search                               ctermbg=8  ctermfg=11 cterm=bold
hi SignColumn                           ctermbg=0
hi SpellBad                             ctermbg=8
hi StatusLine                                                 cterm=bold
hi StatusLineNC                         ctermbg=0  ctermfg=0  cterm=none
hi TabLine                              ctermbg=8  ctermfg=7  cterm=none
hi User1                                ctermbg=3  ctermfg=0  cterm=bold
hi User2                                ctermbg=0  ctermfg=15 cterm=bold
hi VertSplit                            ctermbg=8  ctermfg=8
hi Visual                               ctermbg=7  ctermfg=0

hi ALEVirtualTextError                             ctermfg=1
hi ALEVirtualTextWarning                           ctermfg=4
hi ALEVirtualTextInfo                              ctermfg=5
hi ALEError                             ctermbg=0  ctermfg=1  cterm=underline
hi ALEErrorSign                         ctermbg=0  ctermfg=1
hi ALEWarning                           ctermbg=0  ctermfg=4  cterm=underline
hi ALEWarningSign                       ctermbg=0  ctermfg=4
hi ALEInfo                              ctermbg=0  ctermfg=5  cterm=underline
hi ALEInfoSign                          ctermbg=0  ctermfg=5

hi link DirvishGitModified DiffChange
hi link DirvishGitStaged DiffAdd
hi link DirvishGitRenamed DiffChange
hi link DirvishGitUnmerged DiffChange
hi link DirvishGitIgnored DiffDelete
hi link DirvishGitUntracked DiffChange
hi default link DirvishGitUntrackedDir DirvishPathTail

hi link CtrlPMode2 User1
hi link TabLineFill StatusLineNC
hi link TabLineSel User1

" variable name
hi! Identifier   ctermfg=12 cterm=none
" function name
hi! Function     ctermfg=2  cterm=none
" generic preprocessor
hi! PreProc      ctermfg=5  cterm=none
" preprocessor #include
hi! Include      ctermfg=5  cterm=none
" preprocessor #define
hi! Define       ctermfg=5  cterm=bold
" same as define
hi! Macro        ctermfg=5  cterm=none
" preprocessor #if, #else, #endif, etc.
hi! PreCondit    ctermfg=5  cterm=none
" generic statement
hi! Statement    ctermfg=4  cterm=bold
" if, then, else, endif, swicth, etc.
hi! Conditional  ctermfg=4  cterm=bold
" for, do, while, etc.
hi! Repeat       ctermfg=4  cterm=bold
" case, default, etc.
hi! Label        ctermfg=4  cterm=bold
" try, catch, throw
hi! Exception    ctermfg=4  cterm=bold
" sizeof, "+", "*", etc.
hi! Operator     ctermfg=14 cterm=bold
" any other keyword
hi! Keyword      ctermfg=4  cterm=bold
" generic type
hi! Type         ctermfg=2  cterm=bold
" static, register, volatile, etc
hi! StorageClass ctermfg=2  cterm=none
" struct, union, enum, etc.
hi! Structure    ctermfg=2  cterm=none
" typedef
hi! Typedef      ctermfg=2  cterm=none
" character constant: 'c', '/n'
hi! Character    ctermfg=3  cterm=none
" string constant: "this is a string"
hi! String       ctermfg=3  cterm=none
" generic comment
hi! Comment      ctermfg=8  cterm=none
" generic constant
hi! Constant     ctermfg=7  cterm=none
" boolean constant: TRUE, false
hi! Boolean      ctermfg=7  cterm=bold
" number constant: 234, 0xff
hi! Number       ctermfg=7  cterm=bold
" floating point constant: 2.3e10
hi! Float        ctermfg=7  cterm=bold
